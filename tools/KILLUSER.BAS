
5 DIM UL$(100)
10 CALL CLEAR
20 DISPLAY AT(1,1)BEEP ERASE ALL:"READING USER LIST"
30 C=0
40 OPEN #1:"DSK5.USERS",INPUT ,DISPLAY ,VARIABLE 80
50 IF EOF(1)THEN 90
60 LINPUT #1:UL$(C)
70 C=C+1
80 GOTO 50
90 CLOSE #1
100 DISPLAY AT(1,1)BEEP ERASE ALL:"CALCULATING CUTOFF"
110 OPEN #1:"DSK5.LASTMESS",INPUT ,DISPLAY ,VARIABLE 80
120 LINPUT #1:LM$
130 CLOSE #1
140 N=VAL(SEG$(LM$,4,3))
150 N=N-500
170 FOR A=0 TO C-1
180 DISPLAY AT(1,1)BEEP ERASE ALL:"CHECKING USER "&STR$(A)
190 A$=STR$(A):: IF A<10 THEN A$="0"&A$
200 A$="DSK5.USER"&A$
210 OPEN #1:A$,INPUT ,DISPLAY ,VARIABLE 80
220 INPUT #1:X$,X$,X$,X$
225 CLOSE #1
227 IF SEG$(X$,4,3)="***" THEN X=-800 :: GOTO 240
230 X=VAL(SEG$(X$,4,3))
240 IF ABS(N-X)>500 THEN UL$(A)="*"&UL$(A)
250 NEXT A
260 CALL CLEAR
270 T=0
280 CALL CLEAR :: FOR A=0 TO 20
290 DISPLAY AT(A+1,1):UL$(A+T)
300 NEXT A
310 R=1
320 CALL HCHAR(R,2,ASC(">"))
330 CALL KEY(0,K,S)
340 IF S<>1 THEN 330
350 IF K=69 THEN R=R-1 :: IF R=0 THEN IF T>0 THEN T=T-20 :: R=21 :: GOTO 280
360 IF K=88 THEN R=R+1 :: IF R=22 THEN IF T<80 THEN T=T+20 :: R=1 :: GOTO 280
365 CALL VCHAR(1,2,32,24):: CALL HCHAR(R,2,ASC(">"))
370 IF K=13 THEN 500
380 IF K<>32 THEN 330
390 IF SEG$(UL$(T+R-1),1,1)="*" THEN UL$(T+R-1)=SEG$(UL$(T+R-1),2,80)ELSE UL$(T+R-1)="*"&UL$(T+R-1)
400 DISPLAY AT(R,1):UL$(T+R-1)
410 GOTO 330
500 CALL CLEAR
510 DISPLAY AT(1,1)BEEP ERASE ALL:"REWRITING FILES..."
515 OPEN #1:"DSK5.USERS",OUTPUT,DISPLAY ,VARIABLE 80
520 FOR A=0 TO C-1
530 IF SEG$(UL$(A),1,1)<>"*" THEN PRINT #1:UL$(A)
540 NEXT A
550 DISPLAY AT(1,1)BEEP ERASE ALL:"DELETING DEAD WEIGHT"
560 FOR A=0 TO C-1
570 IF SEG$(UL$(A),1,1)<>"*" THEN 600
575 A$=STR$(A):: IF A<10 THEN A$="0"&A$
580 DELETE "DSK5.USER"&A$
600 NEXT A
610 CALL CLEAR
620 DISPLAY BEEP:"ALL FINISHED. USE DISK":"MANAGER TO RENAME USER":"FILE NUMBERS."
